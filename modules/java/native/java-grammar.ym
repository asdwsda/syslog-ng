/*
 * Copyright (c) 2014 BalaBit IT Ltd, Budapest, Hungary
 * Copyright (c) 2014 Viktor Juhasz <viktor.juhasz@balabit.com>
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License version 2 as published
 * by the Free Software Foundation, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "modjava-parser.h"

}

%code {

#include "cfg-parser.h"
#include "cfg-grammar.h"
#include "plugin.h"

FilterExprNode *last_filter;
JavaPreferences *last_preferences;

}

%name-prefix "java_"
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {gpointer *instance}
%parse-param {gpointer arg}


/* INCLUDE_DECLS */

%token KW_JAVA
%token KW_CLASS_PATH
%token KW_CLASS_NAME
%token KW_OPTION

%type <ptr> java
%type <ptr> java_dest
%type <ptr> java_filter
%type <ptr> java_parser
%type <ptr> java_rewrite

%%

start
        : java
          {
            *instance = $1;
            YYACCEPT;
          }
        ;

java
        : LL_CONTEXT_DESTINATION java_dest { $$ = $2; }
        | LL_CONTEXT_FILTER java_filter { $$ = $2; }
        | LL_CONTEXT_PARSER java_parser { $$ = $2; }
        | LL_CONTEXT_REWRITE java_rewrite { $$ = $2; }
        ;

java_rewrite
        : KW_JAVA
          {
            *instance = last_rewrite = java_rewrite_new(configuration);
            last_preferences = java_rewrite_get_preferences(last_rewrite);
          }
          '(' java_rewrite_options ')'
          {
            $$ = *instance;
          }

java_rewrite_options
        : java_rewrite_option java_rewrite_options
        |
        ;

java_rewrite_option
        : KW_CLASS_PATH '(' string ')'
          {
            java_preferences_set_class_path(last_preferences, $3);
            free($3);
          }
        | KW_CLASS_NAME '(' string ')'
          {
            java_preferences_set_class_name(last_preferences, $3);
            free($3);
          }
        | KW_OPTION '(' java_rewrite_custom_options ')'
        | rewrite_condition_opt
        ;

java_rewrite_custom_options
  : java_rewrite_custom_option java_rewrite_custom_options
  |
  ;

java_rewrite_custom_option
  : LL_STRING LL_STRING { java_preferences_set_option(last_preferences, $1, $2); free($1); free($2); }

java_parser
        : KW_JAVA
          {
            last_parser = *instance = java_parser_new(configuration);
          }
          '(' java_parser_options ')'
          {
            $$ = last_parser;
          }

java_parser_options
        : java_parser_option java_parser_options
        |
        ;

java_parser_option
        : KW_CLASS_PATH '(' string ')'
          {
            java_parser_set_class_path(last_parser, $3);
            free($3);
          }
        | KW_CLASS_NAME '(' string ')'
          {
            java_parser_set_class_name(last_parser, $3);
            free($3);
          }
        | KW_OPTION '(' java_parser_custom_options ')'
        | parser_opt
        ;

java_parser_custom_options
  : java_parser_custom_option java_parser_custom_options
  |
  ;

java_parser_custom_option
  : LL_STRING LL_STRING { java_parser_set_option(last_parser, $1, $2); free($1); free($2); }

java_filter
        : KW_JAVA
          {
            *instance = last_filter = java_filter_new(configuration);
          }
          '(' java_filter_options ')'
          {
            $$ = *instance;
          }
        ;

java_filter_options
        : java_filter_option java_filter_options
        |
        ;

java_filter_option
        : KW_CLASS_PATH '(' string ')'
          {
            java_filter_set_class_path(last_filter, $3);
            free($3);
          }
        | KW_CLASS_NAME '(' string ')'
          {
            java_filter_set_class_name(last_filter, $3);
            free($3);
          }
        | KW_OPTION '(' java_filter_custom_options ')'
        ;

java_filter_custom_options
  : java_filter_custom_option java_filter_custom_options
  |
  ;

java_filter_custom_option
  : LL_STRING LL_STRING { java_filter_set_option(last_filter, $1, $2); free($1); free($2); }

java_dest
        : KW_JAVA
          {
            last_driver = *instance = java_dd_new(configuration);
          }
        '(' java_dest_options ')' { $$ = last_driver; }
        ;

java_dest_options
        : java_dest_option java_dest_options
        |
        ;

java_dest_option
        : KW_CLASS_PATH '(' string ')'
          {
            java_dd_set_class_path(last_driver, $3);
            free($3);
          }
        | KW_CLASS_NAME '(' string ')'
          {
            java_dd_set_class_name(last_driver, $3);
            free($3);
          }
        | KW_TEMPLATE '(' string ')' { java_dd_set_template_string(last_driver, $3); free($3); }
        | KW_OPTION '(' java_dest_custom_options ')'
        | threaded_dest_driver_option
        | dest_driver_option
        | { last_template_options = java_dd_get_template_options(last_driver); } template_option
        ;

java_dest_custom_options
  : java_dest_custom_option java_dest_custom_options
  |
  ;

java_dest_custom_option
  : LL_STRING LL_STRING { java_dd_set_option(last_driver, $1, $2); free($1); free($2); }

/* INCLUDE_RULES */

%%
